pluginManagement {
    repositories {
        if (cacheRedirectorEnabled == 'true') {
            logger.info("Using cache redirector for settings.gradle pluginManagement")
            maven {
                url "https://cache-redirector.jetbrains.com/plugins.gradle.org/m2"
            }
        } else {
            gradlePluginPortal()
        }
    }
}

buildscript {
    repositories {
        if (cacheRedirectorEnabled == 'true') {
            maven { url "https://cache-redirector.jetbrains.com/kotlin.bintray.com/kotlin-dependencies" }
        } else {
            maven { url "https://kotlin.bintray.com/kotlin-dependencies" }
        }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-build-gradle-plugin:0.0.3")
    }
}

def buildProperties = BuildPropertiesKt.getKotlinBuildPropertiesForSettings(settings)

logger.info("buildCacheUrl=${buildProperties.buildCacheUrl}")
logger.info("pushToBuildCache=${buildProperties.pushToBuildCache}")

buildCache {
    if (buildProperties.isTeamcityBuild) {
        local {
            enabled = false
        }
    }

    if (buildProperties.buildCacheUrl != null) {
        remote(HttpBuildCache) {
            url = buildProperties.buildCacheUrl
            push = buildProperties.pushToBuildCache
        }
    }
}

include "prepare-deps"

if (buildProperties.includeCidrPlugins) {
    logger.info("Including CIDR plugins in buildSrc/settings.gradle")
    include ":prepare-deps:kotlin-native-platform-deps"
    project(":prepare-deps:kotlin-native-platform-deps").projectDir =
            file("${buildProperties.propertiesProvider.rootProjectDir}/kotlin-ultimate/buildSrc/prepare-deps/kotlin-native-platform-deps")
} else {
    logger.info("NOT including CIDR plugins in buildSrc/settings.gradle")
}

if (buildProperties.includeUltimate) {
    logger.info("Including extension for IJ Ultimate in buildSrc/settings.gradle")
    include ":prepare-deps:lldb-frontend"
    include ":prepare-deps:native-debug-plugin"
    project(":prepare-deps:lldb-frontend").projectDir =
            file("${buildProperties.propertiesProvider.rootProjectDir}/kotlin-ultimate/buildSrc/prepare-deps/lldb-frontend")
    project(":prepare-deps:native-debug-plugin").projectDir =
            file("${buildProperties.propertiesProvider.rootProjectDir}/kotlin-ultimate/buildSrc/prepare-deps/native-debug-plugin")
} else {
    logger.info("Not including extension for IJ Ultimate in buildSrc/settings.gradle")
}
